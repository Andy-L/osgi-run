import groovy.io.FileType

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.7

def osgiRunVersion = '1.1'

group = 'com.athaydes.gradle.osgi'
version = osgiRunVersion

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar( type: Jar, dependsOn: classes ) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task updateVersion {
    try {
        println "Updating version to $newVersion"
    } catch ( e ) {
        throw new GradleException( "You must provide a newVersion like this: '-PnewVersion=1.0'" )
    }
    def workingDir = new File( '.' ).absoluteFile.parentFile
    if ( workingDir.name != project.name ) {
        throw new GradleException( 'This task must be run from the project root directory' )
    }
    def affectedFiles = [ ]
    workingDir.parentFile.eachFileRecurse( FileType.FILES ) { file ->
        if ( file.name == 'build.gradle' ) {
            def txt = file.text
            if ( txt.contains( 'osgiRunVersion' ) ) {
                affectedFiles << file
                file.write txt.replaceAll( /\ndef\s+osgiRunVersion\s?=\s?.+\n/,
                        "\ndef osgiRunVersion = '$newVersion'\n" )
            }
        }
    }
    println "Affected files:\n- ${affectedFiles.join( '\n- ' )}"
}

/* Publishing config */

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
        }
    }
}

apply plugin: 'com.jfrog.bintray'

bintray {
    user = project.hasProperty( "bintrayUserName" ) ? bintrayUserName : null
    key = project.hasProperty( "bintrayApiKey" ) ? bintrayApiKey : null
    publications = [ "mavenJava" ]
    publish = true
    pkg {
        repo = 'maven'
        name = 'osgi-run-core'
        licenses = [ 'Apache-2.0' ]
        desc = 'A Gradle plugin to make the development of modular applications using OSGi completely painless'
        websiteUrl = 'https://github.com/renatoathaydes/osgi-run'
        issueTrackerUrl = 'https://github.com/renatoathaydes/osgi-run/issues'
        labels = [ 'osgi', 'ipojo', 'modular' ]
        publicDownloadNumbers = true
    }
}

bintrayUpload.dependsOn build, sourcesJar
